apply plugin: 'com.android.application'

android {
    namespace "com.rowlhouse.picasim"
    compileSdkVersion 35

    defaultConfig {
        applicationId "com.rowlhouse.picasim"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared",
                          "-DPICASIM_ENABLE_VR=OFF",
                          "-DANDROID_APP_PLATFORM=android-21"
                abiFilters 'arm64-v8a', 'x86_64'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
            version "3.22.1+"
        }
    }

    // Bundle game data as Android assets
    sourceSets {
        main {
            assets.srcDirs = ['../../data']
            // Include SDL2 Java sources directly
            java.srcDirs = [
                'src/main/java',
                '../../third_party/SDL2/android-project/app/src/main/java'
            ]
        }
    }

    packaging {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    lint {
        abortOnError false
    }
}

// Regenerate asset manifest before every build so new/removed data files
// are always picked up (the manifest drives extraction on first launch).
task generateAssetManifest(type: Exec) {
    def projectRoot = file("${projectDir}/../..")
    commandLine 'python', "${projectRoot}/tools/generate_asset_manifest.py",
            "${projectRoot}/data",
            "${projectRoot}/data/assets_manifest.txt",
            "${projectRoot}/packaging_excludes.txt"
}
preBuild.dependsOn generateAssetManifest

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
